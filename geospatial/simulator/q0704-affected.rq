#title:affected
#comment:

#tags:affected,intersection,connected-points,wkt,coordinates
#color:ex-green
#img:

prefix xsd: <http://www.w3.org/2001/XMLSchema#> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
prefix dbpedia: <http://dbpedia.org/resource/> 
prefix dbo: <http://dbpedia.org/ontology/> 
prefix dbp: <http://dbpedia.org/property/> 
prefix tto: <http://example.org/tuto/ontology#> 
prefix ttr: <http://example.org/tuto/resource#> 
prefix geo: <http://www.opengis.net/ont/geosparql#> 
prefix geof: <http://www.opengis.net/def/function/geosparql/> 
prefix uom: <http://www.opengis.net/def/uom/OGC/1.0/> 
prefix ex: <http://example.org/> 
prefix my: <http://example.org/ApplicationSchema#> 
prefix cim: <http://iec.ch/TC57/CIM-generic#> 
prefix xdmp: <http://marklogic.com/xdmp#> 
prefix custom: <http://www.custom.org/> 

select DISTINCT ?g_wktPolygon ?g_wktLine
where {
  
  #terminals
  ?tmId rdf:type cim:Terminal.
  ?tmId cim:IdentifiedObject.name ?tmName.
   
  #connectivity nodes of terminal
  ?tmId cim:Terminal.ConnectivityNode ?cNodeId.
  ?cNodeId cim:IdentifiedObject.name ?cNodeName.
  
  #conducting equipment of terminal
  ?tmId cim:Terminal.ConductingEquipment ?condEqId.
  ?condEqId cim:IdentifiedObject.name ?condEqName.
  
  #locations
  ?condEqId cim:PowerSystemResource.Location ?locationId.
  ?postionPointId1 rdf:type cim:PositionPoint.
  ?postionPointId2 rdf:type cim:PositionPoint.
  
  ?postionPointId1 cim:PositionPoint.Location ?locationId.
  ?postionPointId2 cim:PositionPoint.Location ?locationId.
  
  #coordinates
  ?postionPointId1 cim:PositionPoint.xPosition ?x1.
  ?postionPointId1 cim:PositionPoint.yPosition ?y1.
  
  ?postionPointId2 cim:PositionPoint.xPosition ?x2.
  ?postionPointId2 cim:PositionPoint.yPosition ?y2.
	
  #hazard zones
  ?hazardType rdfs:subClassOf+ custom:Hazard. 
  ?stormId rdf:type ?hazardType.
  ?stormId custom:Hazard.DangerZone ?g_wktStorm.

  #construct WKTs (draw geometry)
  BIND(concat("POINT (", str(?x1), " ", str(?y1), ")") as ?pointStr)
  BIND(strdt(?pointStr, geo:wktLiteral) as ?g_wktPoint)
  
  BIND(concat("LINESTRING (", str(?x1), " ", str(?y1), ", ", str(?x2), " ", str(?y2), ")") as ?lineStr).
  BIND(strdt(?lineStr, geo:wktLiteral) as ?g_wktLine).
  
  BIND(<http://example.org/custom-function/buffer>(?lineStr, 50) as ?polygonStr).
  BIND(strdt(?polygonStr, geo:wktLiteral) as ?g_wktPolygon).
  
  BIND(geof:intersection(?g_wktPolygon, ?g_wktStorm) as ?g_intersection).
  FILTER(str(?g_intersection) != "POINT EMPTY")
  
  FILTER(?x1 != ?x2 || ?y1 != ?y2)
  FILTER(str(?postionPointId1) < str(?postionPointId2))
}